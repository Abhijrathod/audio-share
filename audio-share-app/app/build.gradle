plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.google.protobuf'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'io.github.mkckr0.audio_share_app'
    compileSdk 34

    defaultConfig {
        applicationId "io.github.mkckr0.audio_share_app"
        minSdk 23
        targetSdk 34
        versionCode 3
        versionName "0.0.6"
        archivesBaseName = "${rootProject.name}-$versionName"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties["storeFile"])
            keyAlias keystoreProperties["keyAlias"]
            storePassword keystoreProperties["storePassword"]
            keyPassword keystoreProperties["keyPassword"]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    kotlin {
        jvmToolchain(17)
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }

    sourceSets {
        main {
            proto {
                srcDir '../../protos'
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }
}

dependencies {

    def nav_version = "2.7.2"

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.protobuf:protobuf-kotlin-lite:3.21.12'
    implementation 'io.netty:netty-all:4.1.96.Final'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'androidx.preference:preference:1.2.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.8'
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                kotlin {
                    option 'lite'
                }
                java {
                    option 'lite'
                }
            }
        }
    }
}
